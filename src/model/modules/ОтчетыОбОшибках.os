#Использовать json

Функция СохранитьОтчет(ФайлОтчета) Экспорт
	
	ИдОтчетаОбОшибке = Строка(Новый УникальныйИдентификатор());
	ХранилищеДанных = МенеджерНастроек.ХранилищеДанных();
	
	Попытка
		ХранилищеДанных.СохранитьФайлОтчета(ИдОтчетаОбОшибке, ФайлОтчета);
	Исключение
		ВызватьИсключение("Не удалось сохранить отчет
			|" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат ИдОтчетаОбОшибке;
	
КонецФункции

Процедура ЗаполнитьОтчетОбОшибке(СтруктураОтчета, ИмяФайла) Экспорт
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.UTF8NoBOM);
	ОтчетОбОшибкеJSON = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	ПарсерJSON = Новый ПарсерJSON;
	ДанныеОтчетаОбОшибке = ПарсерJSON.ПрочитатьJSON(ОтчетОбОшибкеJSON, , , Истина);
	
	ИнфоОКонфигурации = ДанныеОтчетаОбОшибке["configInfo"];
	ИнфоОКлиенте = ДанныеОтчетаОбОшибке["clientInfo"];
	ИнфоОСервере = ДанныеОтчетаОбОшибке["serverInfo"];
	ИнфоОбОшибке = ДанныеОтчетаОбОшибке["errorInfo"];
	
	Инфо = СтруктураОтчета.Информация;
	
	Инфо.Вставить("Дата", ДанныеОтчетаОбОшибке.time);
	Инфо.Вставить("ИнформационнаяБаза", ИнфоОКонфигурации["name"]);
	Инфо.Вставить("ИмяКонфигурации", "Не предоставляется платформой");
	Инфо.Вставить("ВерсияКонфигурации", ИнфоОКонфигурации["version"]);
	Инфо.Вставить("ПлатформаКлиент", ИнфоОКлиенте["platformType"]);
	Инфо.Вставить("ВерсияПлатформы1C", ИнфоОКлиенте["appVersion"]);
	Инфо.Вставить("СУБД", ИнфоОСервере["dbms"]);
	Если ИнфоОбОшибке.applicationErrorInfo.Свойство("stack") Тогда
		Стек = ИнфоОбОшибке.applicationErrorInfo.stack[0];
		ПредставлениеСтека = СформироватьПредставлениеСтека(Стек);
		Инфо.Вставить("Стек", ПредставлениеСтека);
	КонецЕсли;
	
	Ошибки = Неопределено;
	Если ИнфоОбОшибке.applicationErrorInfo.Свойство("errors", Ошибки) Тогда
		ПредставлениеОшибок = СформироватьПредставлениеОшибок(Ошибки);
		Инфо.Вставить("Ошибки", ПредставлениеОшибок);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПредставлениеСтека(Стек)
	
	Результат = "";
	
	Для Каждого ЭлементСтека Из Стек Цикл
		
		Результат = Результат + Символы.ПС + ЭлементСтека;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьПредставлениеОшибок(Ошибки)
	
	Результат = "";
	
	Для Каждого Ошибка Из Ошибки Цикл
		
		Для Каждого ЭлементОшибки Из Ошибка Цикл
			
			Если ТипЗнч(ЭлементОшибки) = Тип("Массив") Тогда
				Для Каждого ЭлементЭлементаОшибки Из ЭлементОшибки Цикл
					Результат = Результат + Символы.ПС + ЭлементЭлементаОшибки;
				КонецЦикла;
			Иначе
				Результат = Результат + Символы.ПС + ЭлементОшибки;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СтруктураДанныхОтчетаОбОшибке() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Информация", Новый Структура());
	Результат.Вставить("Файлы", Новый Массив());
	Результат.Вставить("Скриншот", "");
	
	Возврат Результат;
	
КонецФункции